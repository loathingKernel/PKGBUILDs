# Maintainer: Arvid Norlander <VorpalBlade (at) users DOT noreply DOT github DOT com>

pkgname=ddcui
pkgver=0.6.0
pkgrel=1
pkgdesc='Graphical user interface for ddcutil - control monitor settings'
arch=('i686' 'x86_64')
url='https://github.com/rockowitz/ddcui'
license=('GPL-2.0-or-later')
source=(
	"${pkgname}-${pkgver}.tar.gz::https://github.com/rockowitz/${pkgname}/archive/refs/tags/v${pkgver}.tar.gz"
)
sha256sums=('89a0e33314c7bf046ab09b0e68aa3c758b8eba69f06973987522c75843237c39')
depends=(
	'ddcutil>=2.2.0'
	'glib2'
	'hicolor-icon-theme'
	'qt6-base'
)
makedepends=(
	'cmake'
	'qt6-tools'
)

build() {
	cd "${srcdir}"

	cmake \
		-B "${pkgname}-${pkgver}/build" \
		-S "${pkgname}-${pkgver}" \
		-D CMAKE_INSTALL_PREFIX=/usr \
		-D CMAKE_BUILD_TYPE=Release \
		-D USE_QT6=ON

	cmake --build "${pkgname}-${pkgver}/build"
}

package() {
	cd "${srcdir}"
	DESTDIR="${pkgdir}" cmake --install "${pkgname}-${pkgver}/build"
}
