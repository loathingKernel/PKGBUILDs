# Maintainer: Ondřej Hošek <ondra.hosek@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Maxime de Roucy <maxime.deroucy@gmail.com>
# Contributor: Darwin Bautista <djclue917@gmail.com>

pkgname=lib32-gsm
_pkgbase=gsm
pkgver=1.0.22
pkgrel=2
pkgdesc="Shared libraries for GSM 06.10 lossy speech compression"
arch=('x86_64')
url="http://www.quut.com/gsm/"
license=('custom')
depends=('gsm' 'lib32-glibc')
makedepends=('lib32-gcc-libs')
source=("http://www.quut.com/${_pkgbase}/${_pkgbase}-${pkgver}.tar.gz"
        'gsm-shared.patch')
sha256sums=('f0072e91f6bb85a878b2f6dbf4a0b7c850c4deb8049d554c65340b3bf69df0ac'
            'b959059dccdf2aba260125d43f264f1ce3d14ded631a6af0482e026c8cb2b96c')


prepare() {
  cd "${srcdir}/${_pkgbase}-${pkgver%.*}-pl${pkgver##*.}/"

  patch -p0 -i "${srcdir}/gsm-shared.patch"
}

build() {
  cd "${srcdir}/${_pkgbase}-${pkgver%.*}-pl${pkgver##*.}/"

  # flags for shared lib
  CFLAGS="${CFLAGS} -fPIC"
  make \
    CC="gcc -m32" \
    CCFLAGS="-c ${CFLAGS} -fPIC -Wno-error=incompatible-pointer-types"
}

package() {
  cd "${srcdir}/${_pkgbase}-${pkgver%.*}-pl${pkgver##*.}/"

  # Prepare directories
  install -m755 -d "${pkgdir}"/usr/{bin,lib,lib32,include/gsm,share/{licenses/${_pkgbase},man/man{1,3}}}

  make -j1 \
    CC="gcc -m32" \
    INSTALL_ROOT="${pkgdir}/usr" \
    GSM_INSTALL_LIB="${pkgdir}/usr/lib32" \
    GSM_INSTALL_INC="${pkgdir}/usr/include/gsm" \
    GSM_INSTALL_MAN="${pkgdir}/usr/share/man/man3" \
    TOAST_INSTALL_MAN="${pkgdir}/usr/share/man/man1" \
    install

  # clean directories provided by 64-bit package
  rm -Rf "${pkgdir}"/usr/{bin,include,lib,share}
}
