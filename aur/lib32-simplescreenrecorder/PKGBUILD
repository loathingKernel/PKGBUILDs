# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: Maarten Baert

pkgname=lib32-simplescreenrecorder
pkgver=0.4.4
pkgrel=4
pkgdesc="Record 32-bit OpenGL applications with SimpleScreenRecorder."
arch=("x86_64")
url="https://www.maartenbaert.be/simplescreenrecorder/"
license=("GPL3")
depends=("lib32-libgl" "lib32-glu" "lib32-libx11" "lib32-libxext" "lib32-libxfixes")
makedepends=("git" "cmake")
source=("git+https://github.com/MaartenBaert/ssr.git#tag=$pkgver"
         ffmpeg7.patch::https://patch-diff.githubusercontent.com/raw/MaartenBaert/ssr/pull/1036.patch)
sha256sums=('9c7c3a9984800671bfb280ccbbc91e98d4993ea4465ab8f8c73b9ac65bd4c69c'
            'a1d4a2c1d5cfcf6a327cc98e47e6cd76dd2fd03b33ca9e7fcaba971570fae914')

install=lib32-simplescreenrecorder.install

prepare() {
  cd ssr
  mkdir -p build

  patch -p1 -i ../ffmpeg7.patch # Fix build with ffmpeg 7
}

build() {
  cd ssr/build
  export CMAKE_POLICY_VERSION_MINIMUM=3.5
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  #./configure --prefix=/usr --libdir=/usr/lib32 --disable-ssrprogram --disable-assert
  cmake -DCMAKE_INSTALL_PREFIX="/usr" -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_LIBDIR='lib32' -DWITH_SIMPLESCREENRECORDER=OFF \
    -DENABLE_32BIT_GLINJECT=TRUE ../
  make
}
package() {
  cd ssr/build
  make DESTDIR="${pkgdir}" install
}
