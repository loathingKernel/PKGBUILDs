# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgbase=linux-zen-akasha
pkgver=6.7.6.zen1
pkgrel=2
pkgdesc='Linux ZEN Akasha LLVM LTO'
url='https://github.com/zen-kernel/zen-kernel'
arch=(x86_64)
license=(GPL2)
makedepends=(
  bc
  cpio
  gettext
  libelf
  pahole
  perl
  python
  tar
  xz

  clang llvm lld
)
options=('!strip')
_srcname=linux-${pkgver%.*}
_srctag=v${pkgver%.*}-${pkgver##*.}
source=(
  https://cdn.kernel.org/pub/linux/kernel/v${pkgver%%.*}.x/${_srcname}.tar.{xz,sign}
  $url/releases/download/$_srctag/linux-$_srctag.patch.zst{,.sig}
  config  # the main kernel config file
)
validpgpkeys=(
  ABAF11C65A2970B130ABE3C479BE3E4300411886  # Linus Torvalds
  647F28654894E3BD457199BE38DBBDC86092693E  # Greg Kroah-Hartman
  83BC8889351B5DEBBB68416EB8AC08600F108CDF  # Jan Alexander Steffens (heftig)
)
# https://www.kernel.org/pub/linux/kernel/v6.x/sha256sums.asc
sha256sums=('e489ec0e1370d089b446d565aded7a698093d2b7c4122a18f21edb6ef93d37d3'
            'SKIP'
            'c6bc98b4ca0839519bc3b497d3094df4094ab346cea90dea5d6189e2439eedb1'
            'SKIP'
            'd78c3994a51057cdc1167ddd111252bb3f2dfa7f7b9872505defb732eabac169')
b2sums=('51d6e2304e7a9188a0fec5714276589cb46948157c76a2f4ed3f5e0bf634d94a89ea75251229a86e018767a3367328c16b610d631c78d82663dcd1d904b73385'
        'SKIP'
        '804a6bfbd65c834da19bfc0e763c0c51a5bb56db27afa025f6122ca429cfa63b55ca384c45d70fe0601cd16001daf75981a55be86d71b471b005363229213c3e'
        'SKIP'
        'c8a837ca6b0bef2b658192db8c3cd5f998cbbd657d5cc596ebbda345d95cdadaaefccfa9f79672589ca61848ad3f761de3ef4b3a713296605bf18e78e21a9f97')

export KBUILD_BUILD_HOST=archlinux
export KBUILD_BUILD_USER=$pkgbase
export KBUILD_BUILD_TIMESTAMP="$(date -Ru${SOURCE_DATE_EPOCH:+d @$SOURCE_DATE_EPOCH})"
export KCFLAGS="-pipe"

prepare() {
  cd $_srcname

  echo "Setting version..."
  echo "-$pkgrel" > localversion.10-pkgrel
  echo "${pkgbase#linux}" > localversion.20-pkgname

  local src
  for src in "${source[@]}"; do
    src="${src%%::*}"
    src="${src##*/}"
    src="${src%.zst}"
    [[ $src = *.patch ]] || continue
    echo "Applying patch $src..."
    patch -Np1 < "../$src"
  done

  echo "Setting config..."
  cp ../config .config
  if true; then
  # Enable LTO, it might be the cause of kernel panics
  scripts/config --enable LTO_CLANG_THIN
  echo "-> Native config..."
    # General Setup (phoronix says that 03 is not better)
#    scripts/config --disable CONFIG_ZEN_INTERACTIVE
#    scripts/config --disable CONFIG_CC_OPTIMIZE_FOR_PERFORMANCE_O3
#    scripts/config --enable  CONFIG_CC_OPTIMIZE_FOR_PERFORMANCE
    scripts/config --enable  CONFIG_EXPERT
    # Mitigations
    scripts/config --disable CONFIG_SPECULATION_MITIGATIONS
    # IRQ subsystem
    scripts/config --disable CONFIG_GENERIC_IRQ_INJECTION
    # CPU/Task time and stats accounting
#    scripts/config --enable  CONFIG_PSI_DEFAULT_DISABLED
    # Kernel Performance Events And Counters
    scripts/config --disable CONFIG_SLAB_FREELIST_HARDENED
    scripts/config --disable CONFIG_SLUB_CPU_PARTIAL
    scripts/config --disable CONFIG_PROFILING
    # Processor type and features
    scripts/config --enable  CONFIG_PROCESSOR_SELECT
    scripts/config --disable CONFIG_CPU_SUP_AMD
    scripts/config --disable CONFIG_CPU_SUP_HYGON
    scripts/config --disable CONFIG_CPU_SUP_CENTAUR
    scripts/config --disable CONFIG_CPU_SUP_ZHAOXIN
    scripts/config --disable CONFIG_GENERIC_CPU
    scripts/config --enable  CONFIG_MHASWELL
    scripts/config --set-val CONFIG_NR_CPUS 8
    scripts/config --disable CONFIG_MICROCODE_AMD
    scripts/config --disable CONFIG_NUMA
#    scripts/config --disable CONFIG_HZ_1000
#    scripts/config --enable  CONFIG_HZ_300
#    scripts/config --set-val CONFIG_HZ 300
    scripts/config --disable CONFIG_KEXEC_FILE
    scripts/config --disable CONFIG_CRASH_DUMP
#    scripts/config --disable CONFIG_RANDOMIZE_BASE
#    scripts/config --disable CONFIG_DYNAMIC_MEMORY_LAYOUT
#    scripts/config --set-val CONFIG_PHYSICAL_ALIGN 0x1000000
    # CPU Frequency scaling
    scripts/config --disable CONFIG_CPU_FREQ_DEFAULT_GOV_SCHEDUTIL
    scripts/config --enable  CONFIG_CPU_FREQ_DEFAULT_GOV_PERFORMANCE
    # EFI (Extensible Firmware Interface) Support
#    scripts/config --disable CONFIG_EFI_SOFT_RESERVE    
    # Memory Management options
    scripts/config --disable CONFIG_MEMORY_HOTPLUG
    scripts/config --disable CONFIG_TRANSPARENT_HUGEPAGE_ALWAYS
    scripts/config --enable  CONFIG_TRANSPARENT_HUGEPAGE_MADVISE
    # Device Drivers
    scripts/config --disable CONFIG_PCIEAER_INJECT
    scripts/config --disable CONFIG_PCIE_ECRC
#    scripts/config --disable CONFIG_PCIEASPM_DEFAULT
#    scripts/config --enable  CONFIG_PCIEASPM_PERFORMANCE
    # Graphics support
    scripts/config --disable CONFIG_AGP
    scripts/config --set-val CONFIG_VGA_ARB_MAX_GPUS 4
    # USB Host Controller Drivers
    # (for USB polling rate, xhci doesn't respect usbhid)
#    scripts/config --module  CONFIG_USB_XHCI_HCD
#    scripts/config --module  CONFIG_USB_EHCI_HCD
    # Kernel Testing and Coverage
    scripts/config --enable  CONFIG_MEMTEST
  make LLVM=1 LLVM_IAS=1 olddefconfig
  diff -u ../config .config || :
  cat .config > "${startdir}/configs/config-${pkgver}-${pkgrel}-stage1-native"
  echo "-> Running nconfig..."
  make LLVM=1 LLVM_IAS=1 nconfig
  cat .config > "${startdir}/configs/config-${pkgver}-${pkgrel}-stage2-nconfig"
  echo "-> Running localmodconfig..."
  make LLVM=1 LLVM_IAS=1 LSMOD="${startdir}/modprobed.db" localmodconfig
  make LLVM=1 LLVM_IAS=1 nconfig
  cat .config > "${startdir}/configs/config-${pkgver}-${pkgrel}-stage3-localmod"
  fi

  make -s kernelrelease > version
  echo "Prepared $pkgbase version $(<version)"
}

build() {
  cd $_srcname
  make LLVM=1 LLVM_IAS=1 all
  make LLVM=1 LLVM_IAS=1 -C tools/bpf/bpftool vmlinux.h feature-clang-bpf-co-re=1
}

_package() {
  pkgdesc="The $pkgdesc kernel and modules"
  depends=(
    coreutils
    initramfs
    kmod
  )
  optdepends=(
    'wireless-regdb: to set the correct wireless channels of your country'
    'linux-firmware: firmware images needed for some devices'
  )
  provides=(
    KSMBD-MODULE
    UKSMD-BUILTIN
    VHBA-MODULE
    VIRTUALBOX-GUEST-MODULES
    WIREGUARD-MODULE
  )
  replaces=(
  )

  cd $_srcname
  local modulesdir="$pkgdir/usr/lib/modules/$(<version)"

  echo "Installing boot image..."
  # systemd expects to find the kernel here to allow hibernation
  # https://github.com/systemd/systemd/commit/edda44605f06a41fb86b7ab8128dcf99161d2344
  install -Dm644 "$(make -s image_name)" "$modulesdir/vmlinuz"

  # Used by mkinitcpio to name the kernel
  echo "$pkgbase" | install -Dm644 /dev/stdin "$modulesdir/pkgbase"

  echo "Installing modules..."
  ZSTD_CLEVEL=19 make INSTALL_MOD_PATH="$pkgdir/usr" INSTALL_MOD_STRIP=1 \
    DEPMOD=/doesnt/exist modules_install  # Suppress depmod

  # remove build link
  rm "$modulesdir"/build
}

_package-headers() {
  pkgdesc="Headers and scripts for building modules for the $pkgdesc kernel"
  depends=(pahole)

  cd $_srcname
  local builddir="$pkgdir/usr/lib/modules/$(<version)/build"

  echo "Installing build files..."
  install -Dt "$builddir" -m644 .config Makefile Module.symvers System.map \
    localversion.* version vmlinux tools/bpf/bpftool/vmlinux.h
  install -Dt "$builddir/kernel" -m644 kernel/Makefile
  install -Dt "$builddir/arch/x86" -m644 arch/x86/Makefile
  cp -t "$builddir" -a scripts

  # required when STACK_VALIDATION is enabled
  install -Dt "$builddir/tools/objtool" tools/objtool/objtool

  # required when DEBUG_INFO_BTF_MODULES is enabled
  install -Dt "$builddir/tools/bpf/resolve_btfids" tools/bpf/resolve_btfids/resolve_btfids

  echo "Installing headers..."
  cp -t "$builddir" -a include
  cp -t "$builddir/arch/x86" -a arch/x86/include
  install -Dt "$builddir/arch/x86/kernel" -m644 arch/x86/kernel/asm-offsets.s

  install -Dt "$builddir/drivers/md" -m644 drivers/md/*.h
  install -Dt "$builddir/net/mac80211" -m644 net/mac80211/*.h

  # https://bugs.archlinux.org/task/13146
  install -Dt "$builddir/drivers/media/i2c" -m644 drivers/media/i2c/msp3400-driver.h

  # https://bugs.archlinux.org/task/20402
  install -Dt "$builddir/drivers/media/usb/dvb-usb" -m644 drivers/media/usb/dvb-usb/*.h
  install -Dt "$builddir/drivers/media/dvb-frontends" -m644 drivers/media/dvb-frontends/*.h
  install -Dt "$builddir/drivers/media/tuners" -m644 drivers/media/tuners/*.h

  # https://bugs.archlinux.org/task/71392
  install -Dt "$builddir/drivers/iio/common/hid-sensors" -m644 drivers/iio/common/hid-sensors/*.h

  echo "Installing KConfig files..."
  find . -name 'Kconfig*' -exec install -Dm644 {} "$builddir/{}" \;

  echo "Removing unneeded architectures..."
  local arch
  for arch in "$builddir"/arch/*/; do
    [[ $arch = */x86/ ]] && continue
    echo "Removing $(basename "$arch")"
    rm -r "$arch"
  done

  echo "Removing documentation..."
  rm -r "$builddir/Documentation"

  echo "Removing broken symlinks..."
  find -L "$builddir" -type l -printf 'Removing %P\n' -delete

  echo "Removing loose objects..."
  find "$builddir" -type f -name '*.o' -printf 'Removing %P\n' -delete

  echo "Stripping build tools..."
  local file
  while read -rd '' file; do
    case "$(file -Sib "$file")" in
      application/x-sharedlib\;*)      # Libraries (.so)
        strip -v $STRIP_SHARED "$file" ;;
      application/x-archive\;*)        # Libraries (.a)
        strip -v $STRIP_STATIC "$file" ;;
      application/x-executable\;*)     # Binaries
        strip -v $STRIP_BINARIES "$file" ;;
      application/x-pie-executable\;*) # Relocatable binaries
        strip -v $STRIP_SHARED "$file" ;;
    esac
  done < <(find "$builddir" -type f -perm -u+x ! -name vmlinux -print0)

  echo "Stripping vmlinux..."
  strip -v $STRIP_STATIC "$builddir/vmlinux"

  echo "Adding symlink..."
  mkdir -p "$pkgdir/usr/src"
  ln -sr "$builddir" "$pkgdir/usr/src/$pkgbase"
}

_package-docs() {
  pkgdesc="Documentation for the $pkgdesc kernel"

  cd $_srcname
  local builddir="$pkgdir/usr/lib/modules/$(<version)/build"

  echo "Installing documentation..."
  local src dst
  while read -rd '' src; do
    dst="${src#Documentation/}"
    dst="$builddir/Documentation/${dst#output/}"
    install -Dm644 "$src" "$dst"
  done < <(find Documentation -name '.*' -prune -o ! -type d -print0)

  echo "Adding symlink..."
  mkdir -p "$pkgdir/usr/share/doc"
  ln -sr "$builddir/Documentation" "$pkgdir/usr/share/doc/$pkgbase"
}

pkgname=(
  "$pkgbase"
  "$pkgbase-headers"
)
for _p in "${pkgname[@]}"; do
  eval "package_$_p() {
    $(declare -f "_package${_p#$pkgbase}")
    _package${_p#$pkgbase}
  }"
done

# vim:set ts=8 sts=2 sw=2 et:
