# Maintainer: loathingkernel
# Contributor: Torge Matthies <openglfreak at googlemail dot com>
# Contributor: Krzysztof Bogacki <krzysztof dot bogacki at leancode dot pl>

pkgbase=winesync
pkgname=(winesync-dkms winesync-header winesync-common)
pkgver=5.16
pkgrel=8
pkgdesc="Wine synchronization primitive driver"
arch=(x86_64)
url='https://repo.or.cz/linux/zf.git/shortlog/refs/heads/winesync4'
license=('GPL2')
_commit=9ac10c6e711ec096274ecc676ae83a7cf2a1213f
source=("winesync.c-$_commit::https://repo.or.cz/linux/zf.git/blob_plain/$_commit:/drivers/misc/winesync.c"
        "winesync.h-$_commit::https://repo.or.cz/linux/zf.git/blob_plain/$_commit:/include/uapi/linux/winesync.h"
        'winesync.conf'
        '99-winesync.rules'
        'Makefile'
        'dkms.conf')
sha256sums=('0da3cfeb8ab2f6b16083d92e7fa9a526a402802cd2a114d86a8a6947a42e8be8'
            'd21b36d59c6cd69190fe51b6e60505464053cf5450b05a1625bbfcf9b0f26620'
            '609245bd67180eac14c3b2db71ecb161d3cf51e63ae317be8a053df73d5d6fc1'
            '9b22d9976a83785e6a1cfc4a3aa230a8c5e4e903730bbafc598ec86bfaa35c3e'
            '7d58c9ab1b577a3aa79ba4ba2051c905162b2266a4abec7cf04270099ebfeea5'
            '118cb4912a7978e9cca3c14867080cbe2d4a4b610f02907bbee05865ef2bbfbb')

prepare() {
    sed -i -e "s/@PACKAGE_VERSION@/$pkgver/g" "$srcdir/dkms.conf"
}

package_winesync-dkms() {
    pkgdesc+=" - out-of-tree module"
    depends=(dkms winesync-common)
    optdepends=(
        'winesync-header: Allow wine to be compiled with winesync support'
    )
    provides=(WINESYNC-MODULE)
    conflicts=(winesync)

    install -Dm644 "$srcdir/Makefile" "$pkgdir/usr/src/$pkgbase-$pkgver/Makefile"
    install -Dm644 "$srcdir/winesync.h-$_commit" "$pkgdir/usr/src/$pkgbase-$pkgver/include/uapi/linux/winesync.h"
    install -Dm644 "$srcdir/winesync.c-$_commit" "$pkgdir/usr/src/$pkgbase-$pkgver/drivers/misc/winesync.c"
    install -Dm644 "$srcdir/dkms.conf" "$pkgdir/usr/src/$pkgbase-$pkgver/dkms.conf"
}

package_winesync-header() {
    pkgdesc+=" - linux api header file"

    install -Dm644 "$srcdir/winesync.h-$_commit" "$pkgdir/usr/include/linux/winesync.h"
}

package_winesync-common() {
    pkgdesc+=" - common files"
    provides=(winesync-udev-rule)
    conflicts=(winesync-udev-rule)
    replaces=(winesync-udev-rule)

    install -Dm644 "$srcdir/winesync.conf" "$pkgdir/usr/lib/modules-load.d/winesync.conf"
    install -Dm644 "$srcdir/99-winesync.rules" "$pkgdir/usr/lib/udev/rules.d/99-winesync.rules"
}

