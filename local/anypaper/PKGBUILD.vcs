# Maintainer: John "ShaggyTwoDope" Jenkins <twodopeshaggy at gmail dot com>
pkgname=anypaper
pkgver=2.4.r9.g7349a94
pkgrel=1
pkgdesc="Frontend for feh and other wallpaper setting tools."
arch=('x86_64')
url="http://anypaper.sourceforge.net"
license=('GPL')
depends=('gtk2-compat' 'libpng' 'feh')
makedepends=('git' 'automake' 'autoconf' 'gtk-doc')
source=('git+https://github.com/loathingKernel/anypaper.git')
md5sums=('SKIP')

pkgver() {
  cd "$pkgname"

  git describe --long --tags 2>/dev/null | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd "$pkgname"

  autoreconf -f -i
  ./configure --prefix=/usr
  make
}

package() {
  cd "$pkgname"

  make DESTDIR="$pkgdir" install
}

