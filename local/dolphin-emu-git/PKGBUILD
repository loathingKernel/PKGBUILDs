# Maintainer: schuay <jakob.gruber@gmail.com>
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Jeremy Newton (Mystro256) <alexjnewt@gmail.com>

pkgbase=dolphin-emu-git
pkgname=(
  dolphin-emu-git
  dolphin-emu-tool-git
)
pkgver=2509.r213.06b4c89c51
pkgrel=1
epoch=1
pkgdesc='A Gamecube and Wii emulator'
arch=(x86_64)
url=https://dolphin-emu.org
license=(GPL-2.0-or-later)
depends=(
  bluez-libs
  bzip2
  enet
  gcc-libs
  glibc
  hidapi
  libavcodec.so
  libavformat.so
  libavutil.so
  libcurl.so
  #libfmt.so
  libgl
  #libsfml-network.so
  #libsfml-system.so
  libspng.so
  libswscale.so
  libusb-1.0.so
  libx11
  libxi
  libxrandr
  lz4
  lzo
  mbedtls2
  pugixml
  sdl2
  speexdsp
  xxhash
  xz
  zstd
)
makedepends=(
  alsa-lib
  cmake
  git
  glslang
  libevdev
  libminiupnpc.so
  libpulse
  libudev.so
  ninja
  python
  qt6-base
  qt6-svg
  #sfml
)
optdepends=('pulseaudio: PulseAudio backend')
options=(!emptydirs !lto)
source=(
  dolphin-emu::git+https://github.com/dolphin-emu/dolphin.git
  cmake-mgba.diff
)
b2sums=('SKIP'
        '442983a8887078ead6b503aa92f8d93a5d07e747792003775d15a4abe3e394a914e45abe25d00c39b7ad9a6d3f982af5d8a7055a15e95fa3b53d040b5167b217')

prepare() {
  cd dolphin-emu
  git remote set-url origin https://github.com/dolphin-emu/dolphin.git
  git submodule update --init --filter=tree:0 --recursive
  patch -Np1 -i ${srcdir}/cmake-mgba.diff
}

pkgver() {
  cd dolphin-emu
  git describe --long --tags | sed 's/-/.r/; s/-g/./'
}

build() {
  export CFLAGS+=' -I/usr/include/mbedtls2'
  export CXXFLAGS+=' -I/usr/include/mbedtls2'
  export LDFLAGS+=' -L/usr/lib/mbedtls2'
  cmake -S dolphin-emu -B build -G Ninja \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_SKIP_RPATH=ON \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DDISTRIBUTOR=archlinux.org \
    -DQT_NO_PRIVATE_MODULE_WARNING=ON \
    -DENABLE_ANALYTICS=OFF \
    -DENABLE_AUTOUPDATE=OFF \
    -DENABLE_LTO=OFF \
    -DENABLE_TESTS=OFF \
    -DUSE_SYSTEM_FMT=OFF \
    -DUSE_MGBA=ON \
    -Wno-dev
  cmake --build build
}

package_dolphin-emu-git() {
  depends+=(
    alsa-lib
    hicolor-icon-theme
    libevdev
    libminiupnpc.so
    libpulse
    libudev.so
    qt6-base
    qt6-svg
  )
  provides=(dolphin-emu)
  conflicts=(dolphin-emu)

  DESTDIR="${pkgdir}" cmake --install build
  install -Dm 644 dolphin-emu/Data/51-usb-device.rules -t "${pkgdir}"/usr/lib/udev/rules.d/
  rm -rf "${pkgdir}"/usr/{bin/dolphin-tool,include,lib/libdiscord-rpc.a}
}

package_dolphin-emu-tool-git() {
  provides=(dolphin-emu-tool)
  conflicts=(dolphin-emu-tool)
  install -Dm 755 build/Binaries/dolphin-tool -t "${pkgdir}"/usr/bin/
}

# vim: ts=2 sw=2 et:
