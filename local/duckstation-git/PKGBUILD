# Contributor: katt <magunasu.b97@gmail.com>

pkgname=duckstation-git
pkgver=0.1.r9584.gc1bb183
pkgdesc='A Sony PlayStation (PSX) emulator, focusing on playability, speed, and long-term maintainability (git version)'
pkgrel=1
arch=(x86_64 aarch64)
url=https://github.com/stenzek/duckstation
license=(CC-BY-NC-ND-4.0)
depends=(
    sh
    glibc
    gcc-libs
    sdl3
    qt6-base
    libbacktrace.so
    libwebp.so
    libjpeg.so
    libpng16.so
    libxrandr
    libx11
    freetype2 libfreetype.so
    libzstd.so
    libz.so
    libzip.so
    libdbus-1.so
    libcurl.so
    systemd-libs libudev.so
    hicolor-icon-theme
    libglvnd
    glslang
    spirv-tools
)
makedepends=(
    git
    cmake
    clang
    lld
    llvm
    extra-cmake-modules
    ffmpeg
    qt6-tools
    qt6-wayland
    libdrm
    libpulse
    alsa-lib
    sndio
    ninja
    jack
    python
    spirv-headers
)
optdepends=(
    'libdrm'
    'qt6-wayland: Wayland support'
    'libpulse: Pulseaudio support'
    'sndio: Small audio support'
    'alsa-lib: ALSA support'
    'jack: JACK support'
    'ffmpeg: Video capture support'
)
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
options=(!lto)

source=(
    git+https://github.com/stenzek/duckstation.git
    "https://github.com/duckstation/chtdb/releases/download/latest/cheats.zip"
    "https://github.com/duckstation/chtdb/releases/download/latest/patches.zip"
    duckstation-qt.sh
)
noextract=(
    "cheats.zip"
    "patches.zip"
)
sha256sums=('SKIP'
            '46bfe7774f0c93645ea1cffcb259340f72eb2217f8b9479aa3b69a2effd450e4'
            'b054de365ae4ec5ebd633d473b7790227f91f751997296ee6422bdff46531f04'
            '5ded08b875d83afa70526f05d1c4381d8c2991a3224618639210fd2273ad00a7')

pkgver() {
    cd "${srcdir}/${pkgname%-git}"
    git describe --long --abbrev=7 | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
    cd "${srcdir}/${pkgname%-git}"
    cp "${srcdir}"/cheats.zip "${srcdir}"/patches.zip data/resources
}

build() {

    ./duckstation/scripts/deps/build-dependencies-linux.sh \
      -system-freetype -system-harfbuzz -system-libjpeg \
      -system-libpng -system-libwebp -system-libzip \
      -system-zlib -system-zstd -system-qt \
      "$PWD"/build-deps

    unset DEBUGINFOD_URLS
    cmake -B build -S duckstation \
        -G Ninja \
        -Wno-dev \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_C_COMPILER=clang \
        -DCMAKE_CXX_COMPILER=clang++ \
        -DCMAKE_EXE_LINKER_FLAGS_INIT="-fuse-ld=lld" \
        -DCMAKE_MODULE_LINKER_FLAGS_INIT="-fuse-ld=lld" \
        -DCMAKE_SHARED_LINKER_FLAGS_INIT="-fuse-ld=lld" \
        -DCMAKE_INTERPROCEDURAL_OPTIMIZATION=ON \
        -DCMAKE_PREFIX_PATH="$PWD"/build-deps \
        -DALLOW_INSTALL=ON \
        -DINSTALL_SELF_CONTAINED=ON \
        -DCMAKE_INSTALL_PREFIX="${pkgdir}"/opt/duckstation
    ninja -C build
}

package() {
    ninja -C build install

    # install alias
    install -Dm755 "${srcdir}"/duckstation-qt.sh "${pkgdir}/usr/bin/duckstation-qt"

    # install desktop file and icon
    install -Dm644 duckstation/scripts/packaging/org.duckstation.DuckStation.desktop "${pkgdir}/usr/share/applications/org.duckstation.DuckStation.desktop"
    install -Dm644 duckstation/scripts/packaging/org.duckstation.DuckStation.png "${pkgdir}/usr/share/icons/hicolor/512x512/apps/org.duckstation.DuckStation.png"

    # install license
    install -Dm644 duckstation/LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    install -Dm644 duckstation/data/resources/thirdparty.html "${pkgdir}/usr/share/licenses/${pkgname}/thirdparty.html"
}

