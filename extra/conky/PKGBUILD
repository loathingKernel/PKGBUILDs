# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: James Rayner <james@archlinux.org>
# Contributor: Partha Chowdhury <kira.laucas@gmail.com>

pkgname=conky
pkgver=1.22.0
pkgrel=1
pkgdesc='Light-weight system monitor for X, Wayland, and other things, too'
url='https://github.com/brndnmtthws/conky'
license=(
  'BSD-3-Clause'
  'GPL-3.0-or-later'
)
arch=('x86_64')
makedepends=(
  'catch2'
  'cmake'
  'docbook2x'
  'docbook-xsl'
  'git'
  'man-db'
  'pandoc'
  'python-yaml'
  'python-jinja'
  'wayland-protocols'
  'gperf'
)
depends=(
  'cairo'
  'curl' libcurl.so
  'fontconfig'
  'gcc-libs'
  'glib2'
  'glibc'
  'hicolor-icon-theme'
  'imlib2'
  'libpulse' libpulse.so
  'libx11'
  'libxdamage'
  'libxext'
  'libxfixes'
  'libxft'
  'libxinerama'
  'libxml2'
  'tolua++'
  'librsvg'
  'libxnvctrl' libXNVCtrl.so
  'lua'
  'ncurses' libncursesw.so
  'pango'
  'systemd-libs' libsystemd.so
  'wayland'
  'wireless_tools'
)
source=(
  "git+$url.git#tag=v${pkgver}?signed"
  "use-system-cmake.patch"
)
sha512sums=('db1344b31e035d9f38eaceaba44f40d3f4954ae64190e1f5e0cad6b10de9a188c264720644a204c5c560e07a5838e63a261c7146d9981e17380fdd6d4a468017'
            '5bdcef1172e53e5d4344bccf33250227f7e1e52b97e8ed95e226fb5ca2b3dde6da9607d2dd0db5d1cf69a972d315e27e534e4a9732880a3670956e8ee988120d')
b2sums=('44ab19b432550638993c10814b9f00de2bb65daff24a7e56a78e3228bd98a52c24e0853b4248cbb60ab49e79efaac4877fcf3846186b0c18deea6ae0715cce6b'
        '9ce6d4fe9ad315d5b74f19208c983c917e21c3f2475f4abe53f5df1e5258c298c64f8e26ecc97d84b1465efe6cb425ec2d7510b1ae6526479c1f2798a4f32c6d')
validpgpkeys=(
  'AF310802C8EFA20499CF3556137B7AC2BDFD8DF0' # Brenden Matthews <github@brenden.brndn.io>
)

pkgver() {
  cd ${pkgname}
  git describe --long --tags --match 'v*' | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd ${pkgname}
  patch -Np1 -i ../use-system-cmake.patch
}

build() {
  cd ${pkgname}
  cmake \
    -B build \
    -D CMAKE_BUILD_TYPE=None \
    -D CMAKE_CXX_FLAGS="$CXXFLAGS -ffat-lto-objects" \
    -D MAINTAINER_MODE=OFF \
    -D BUILD_TESTS=ON \
    -D BUILD_DOCS=ON \
    -D BUILD_EXTRAS=ON \
    -D BUILD_WLAN=ON \
    -D BUILD_XDBE=ON \
    -D BUILD_XSHAPE=ON \
    -D BUILD_IMLIB2=ON \
    -D BUILD_CURL=ON \
    -D BUILD_RSS=ON \
    -D BUILD_NVIDIA=ON \
    -D BUILD_PULSEAUDIO=ON \
    -D BUILD_JOURNAL=ON \
    -D BUILD_WAYLAND=ON \
    -D BUILD_ICONV=ON \
    -D BUILD_LUA_CAIRO=ON \
    -D BUILD_LUA_IMLIB2=ON \
    -D BUILD_LUA_RSVG=ON \
    -D CMAKE_INSTALL_PREFIX=/usr \
    -Wno-dev \
    -S .
  cmake --build build
}

package() {
  cd ${pkgname}
  DESTDIR="${pkgdir}" cmake --install build
  install -vDm 644 COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -vDm 644 build/extras/vim/syntax/conkyrc.vim -t "${pkgdir}"/usr/share/vim/vimfiles/syntax
  install -vDm 644 extras/vim/ftdetect/conkyrc.vim -t "${pkgdir}"/usr/share/vim/vimfiles/ftdetect
  install -vDm 644 build/extras/nano/conky.nanorc -t "${pkgdir}"/usr/share/nano
  install -vDm 644 extras/gedit/conky.lang -t "${pkgdir}"/usr/share/gtksourceview-3.0/language-specs
}

# vim: ts=2 sw=2 et:
