# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Maintainer: Giancarlo Razzolini <grazzolini@archlinux.org>
# Contributor: Samuel "scrufulufugus" Monson <smonson@irbash.net>
# Contributor: PedroHLC <root@pedrohlc.com>

pkgname=gamescope
pkgver=3.16.4
pkgrel=1
pkgdesc='SteamOS session compositing window manager'
arch=(x86_64)
url=https://github.com/ValveSoftware/gamescope
license=(
  BSD-2-Clause
  BSD-3-Clause
  LicenseRef-Reshade
)
depends=(
  gcc-libs
  glibc
  lcms2
  libavif
  libcap.so
  libdecor
  libdrm
  libei
  libinput
  libpipewire-0.3.so
  libx11
  libxcb
  libxcomposite
  libxcursor
  libxdamage
  libxext
  libxfixes
  libxi
  libxkbcommon.so
  libxmu
  libxrender
  libxres
  libxtst
  libxxf86vm
  luajit
  pixman
  sdl2
  seatd
  systemd-libs
  vulkan-icd-loader
  wayland
  xcb-util-errors
  xcb-util-wm
  xorg-server-xwayland
)
makedepends=(
  benchmark
  cmake
  git
  glslang
  meson
  ninja
  vulkan-headers
  wayland-protocols
)
source=(
  git+https://github.com/ValveSoftware/gamescope.git#commit=${pkgver}
  https://github.com/ValveSoftware/openvr/pull/1890.patch
  https://github.com/ValveSoftware/gamescope/commit/6a99868.patch
)
b2sums=('e773a84decd8664f7f641a9cf43d75eefbe4d653d8b31dfbb136f73007e09799213597947a70454a6983d615a06813039fa455943b954c1e11a4da843e811b1a'
        'ca715d31473649b6e342d75f44c8cf22cb4c303d546718085a18f00afa2e32532f478261c8c150bd0799005b6742a993575c1914575f5da39ae551315f8cac31'
        '9d667f65de94a75c7367bce03407faaefd0fbd4ecf670296e0ea3fae81f2152f5440c3e0c8e1be7890523d1f60c4b7ec2c624565616b7482505830378f428231')

prepare() {
  cd gamescope

  meson subprojects download

  # Explicitly set origin URL for submodules using relative paths
  git remote set-url origin https://github.com/ValveSoftware/gamescope.git
  git submodule update --init --filter=tree:0 --recursive

  # Fix HDR: https://github.com/hyprwm/Hyprland/issues/9064#issuecomment-2849051937
  patch -Np1 -R -i ${srcdir}/6a99868.patch

  cd subprojects/openvr
  patch -Np1 -i ${srcdir}/1890.patch
}

pkgver() {
  cd gamescope
  git describe --tags | sed 's/-//'
}

build() {
  arch-meson gamescope build \
    -Dforce_fallback_for=glm,stb,libdisplay-info,libliftoff,vkroots,wlroots \
    -Dpipewire=enabled
  meson compile -C build
}

package() {
  DESTDIR="${pkgdir}" meson install -C build \
    --skip-subprojects
  install -Dm 644 gamescope/LICENSE -t "${pkgdir}"/usr/share/licenses/gamescope/
}

# vim: ts=2 sw=2 et:
