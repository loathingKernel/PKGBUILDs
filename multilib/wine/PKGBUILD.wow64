# Maintainer: Peter Jung <ptr1337@archlinux.org>
# Contributor: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: Jan "heftig" Steffens <jan.steffens@gmail.com>
# Contributor: Eduardo Romero <eduardo@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>

pkgname=wine
pkgver=11.2
pkgrel=1
_tkg_commit=2d624ba32f04b6aa7d13ecbda8f477dff12411c3

_pkgbasever=${pkgver/rc/-rc}

source=("git+https://gitlab.winehq.org/wine/wine.git?signed#tag=wine-$_pkgbasever"
        30-win32-aliases.conf
        wine-binfmt.conf
        ntsync.conf)
source+=(
        compatibilitytool.vdf
        toolmanifest.vdf
        miniproton
        git+https://github.com/Frogging-Family/wine-tkg-git.git#commit=${_tkg_commit}
        wine-sauce.patch
)
sha512sums=('763e0ef85de694edc189649d14fbb9ada9de24031875e8bf5789c95022efc30bcba287d3d85be23288e433ac65ed0fb80e3959b31d1e1de424b84d7d151abaa3'
            '6e54ece7ec7022b3c9d94ad64bdf1017338da16c618966e8baf398e6f18f80f7b0576edf1d1da47ed77b96d577e4cbb2bb0156b0b11c183a0accf22654b0a2bb'
            'bdde7ae015d8a98ba55e84b86dc05aca1d4f8de85be7e4bd6187054bfe4ac83b5a20538945b63fb073caab78022141e9545685e4e3698c97ff173cf30859e285'
            'ac2bd634838ffe6b90f2637e229013f0993fc1013271dbeefd216dc262a8bb79e4a5ce15a75cbfcb0c3b521d32f4ebe1ed25a6b066b99cc327b60bd6d7212e6f'
            '47eb18050d05500374d168277552a70b3878dda408b7ae7385a9225f516d48759f79370bb6644e4b9a7e435b6f8bda9fc57a68bb4694a2368151a6c46f6b49dd'
            '61e0176986a9794855eb89367b123413039c5c4a8a16ce9bdc65abc7668ae48e7d4fd9feeeb4d300888998d44bb5c457f19a8defb482edefd719a7dfdced3235'
            '5af31424909cda08a52daa31cd3baa436926953fb8f23348ae71c5d2f7aa353ae47febded60765c785d364d57d89c2fb5fbc5191bb3b02758fa511c6adb54070'
            '517605d99f6c4cb68d63e4f16b900d8c1b6535ce69df91e5ef652266b73616ccce89c6c2941fa87aac94768f1f95b846d189b73302c2425cce61b5a1a8749ddf'
            '950da53c1f1816136f9ef5505759f535b72f61b0f25bb296e877a5279a87b95d6d97b34bb7bdf3e1255aca84a0696187d521cd6ea7ccd76a9da361d250ffc36f')
validpgpkeys=(5AC1A08B03BD7A313E0A955AF5E6E9EEB9461DD7
              DA23579A74D4AD9AF9D3F945CEFAC8EAAF17519D)

pkgdesc="A compatibility layer for running Windows programs"
url="https://www.winehq.org"
arch=(x86_64)
options=(!staticlibs !lto !debug pestrip)
license=(LGPL-2.1-or-later)
depends=(
  attr
  desktop-file-utils
  fontconfig
  freetype2
  gcc-libs
  gettext
  libpcap
  libunwind
  libxcursor
  libxkbcommon
  libxi
  libxrandr
  wayland
)
makedepends=(
  alsa-lib
  ffmpeg
  git
  gnutls
  gst-plugins-base-libs
  libcups
  libgphoto2
  libpulse
  libva
  libxcomposite
  libxinerama
  libxxf86vm
  mesa
  mingw-w64-gcc
  opencl-headers
  opencl-icd-loader
  pcsclite
  perl
  python
  samba
  sane
  sdl2
  unixodbc
  v4l-utils
  vulkan-headers
  vulkan-icd-loader
)
optdepends=(
  alsa-lib
  alsa-plugins
  cups
  dosbox
  ffmpeg
  gnutls
  gst-plugins-bad
  gst-plugins-base
  gst-plugins-base-libs
  gst-plugins-good
  gst-plugins-ugly
  libgphoto2
  libpulse
  libva
  libxcomposite
  libxinerama
  opencl-icd-loader
  pcsclite
  samba
  sane
  sdl2
  unixodbc
  v4l-utils
  vulkan-icd-loader
  wine-gecko
  wine-mono
)
optdepends+=(
  NTSYNC-MODULE
)
install=wine.install

prepare() {
  # Get rid of old build dirs
  rm -rf $pkgname-64-build
  mkdir $pkgname-64-build

  cd wine
  patch -Np1 -i "$srcdir"/wine-sauce.patch
  _tkg_path="$srcdir"/wine-tkg-git/wine-tkg-git/wine-tkg-patches
  patch -Np1 -i "$_tkg_path"/proton/LAA/LAA-unix-wow64.patch
  patch -Np1 -i "$_tkg_path"/proton/msvcrt_nativebuiltin/msvcrt_nativebuiltin_mainline.patch
  patch -Np1 -i "$_tkg_path"/proton/proton-win10-default/proton-win10-default.patch
  ./tools/make_requests
  ./tools/make_specfiles
  ./dlls/winevulkan/make_vulkan
  autoreconf -fiv
  rm -rf autom4te.cache
}

build() {
  # Apply flags for cross-compilation
  export CROSSCFLAGS="-O2 -pipe -g"
  export CROSSCXXFLAGS="-O2 -pipe -g"
  export CROSSLDFLAGS="-Wl,-O1"

  local -a split=($CFLAGS)
  local -A flags
  for opt in "${split[@]}"; do flags["${opt%%=*}"]="${opt##*=}"; done
  local march="${flags["-march"]:-nocona}"
  local mtune="${flags["-mtune"]:-core-avx2}"

  # From Proton
  OPTIMIZE_FLAGS="-O2 -march=$march -mtune=$mtune -mfpmath=sse -pipe"
  OPTIMIZE_FLAGS+=" -mprefer-avx128 -fvect-cost-model=cheap -fipa-pta"
  SANITY_FLAGS="-fwrapv -fno-strict-aliasing -D_TIME_BITS=64 -D_FILE_OFFSET_BITS=64"
  DEBUG_FLAGS="-ffunction-sections -fdata-sections -fno-omit-frame-pointer"
  WARNING_FLAGS="-Wno-incompatible-pointer-types"
  STRIP_FLAGS="-s"
  COMMON_FLAGS="$OPTIMIZE_FLAGS $SANITY_FLAGS $DEBUG_FLAGS $WARNING_FLAGS $STRIP_FLAGS"
  #LTO_CFLAGS="-fuse-linker-plugin -fdevirtualize-at-ltrans -flto-partition=one -flto -Wl,-flto"

  COMMON_LDFLAGS="-Wl,-O1,--sort-common,--as-needed"
  #LTO_LDFLAGS="$LTO_CFLAGS"

  # Disable assertions
  #export CPPFLAGS="-U_FORTIFY_SOURCE -D_FORTIFY_SOURCE=0 -DNDEBUG -D_NDEBUG"

  export LDFLAGS="$COMMON_LDFLAGS $LTO_LDFLAGS"
  export CROSSLDFLAGS="$COMMON_LDFLAGS -Wl,--file-alignment,4096"

  export CFLAGS="$COMMON_FLAGS -mcmodel=small $LTO_CFLAGS"
  export CXXFLAGS="$COMMON_FLAGS -mcmodel=small -std=c++17 $LTO_CFLAGS"
  export CROSSCFLAGS="$COMMON_FLAGS"
  export CROSSCXXFLAGS="$COMMON_FLAGS -std=c++17"
  export PKG_CONFIG_PATH="/usr/lib/pkgconfig:/usr/share/pkgconfig"
  cd "$srcdir/$pkgname-64-build"
  ../wine/configure \
    --prefix=/usr \
    --libdir=/usr/lib \
    --with-x \
    --with-wayland \
    --with-gstreamer \
    --with-freetype \
    --with-mingw \
    --with-alsa \
    --with-ffmpeg \
    --without-oss \
    --disable-tests \
    --enable-archs=x86_64,i386 \
    --enable-build-id

  make
}

package() {
  cd "$srcdir/$pkgname-64-build"
  make prefix="$pkgdir/usr" \
    libdir="$pkgdir/usr/lib" \
    dlldir="$pkgdir/usr/lib/wine" install-lib

  # Font aliasing settings for Win32 applications
  install -d "$pkgdir"/usr/share/fontconfig/conf.{avail,default}
  install -m644 "$srcdir/30-win32-aliases.conf" "$pkgdir/usr/share/fontconfig/conf.avail"
  ln -s ../conf.avail/30-win32-aliases.conf "$pkgdir/usr/share/fontconfig/conf.default/30-win32-aliases.conf"
  install -Dm 644 "$srcdir/wine-binfmt.conf" "$pkgdir/usr/lib/binfmt.d/wine.conf"

  i686-w64-mingw32-strip --strip-unneeded "$pkgdir"/usr/lib/wine/i386-windows/*.{dll,exe,cpl}
  x86_64-w64-mingw32-strip --strip-unneeded "$pkgdir"/usr/lib/wine/x86_64-windows/*.{dll,exe,cpl}

  find "$pkgdir"/usr/lib/wine -iname "*.a" -delete
  find "$pkgdir"/usr/lib/wine -iname "*.def" -delete

  install -d "$pkgdir/usr/share/steam/compatibilitytools.d/miniproton"
  install -m644 "$srcdir/compatibilitytool.vdf" "$pkgdir/usr/share/steam/compatibilitytools.d/miniproton"
  install -m644 "$srcdir/toolmanifest.vdf" "$pkgdir/usr/share/steam/compatibilitytools.d/miniproton"
  install -m755 "$srcdir/miniproton" "$pkgdir/usr/share/steam/compatibilitytools.d/miniproton"

  # Load ntsync module
  install -Dm644 "$srcdir/ntsync.conf" "$pkgdir/usr/lib/modules-load.d/10-ntsync.conf"
}

# vim:set ts=8 sts=2 sw=2 et:
