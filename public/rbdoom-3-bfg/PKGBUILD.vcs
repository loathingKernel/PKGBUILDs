# Maintainer: loathingkernel <username at gmail dot com>
# Contributor: Mike Swanson <mikeonthecomputer@gmail.com>
# Based on the rbdoom3-bfg-git package by M0Rf30

pkgname=rbdoom-3-bfg
pkgver=1.6.0.r64.8184ab7e
pkgrel=1
pkgdesc="Doom 3 BFG Edition with modern engine features like PBR, Baked Global Illumination, Soft Shadows"
arch=('x86_64')
url="https://github.com/RobertBeckebans/RBDOOM-3-BFG"
license=('GPL3')
depends=(
  openal
  sdl2
  vulkan-icd-loader
)
makedepends=(
  clang
  cmake
  git
  glu
  ispc
  ninja
  vulkan-headers
)
optdepends=(
  'doom3bfg-data: packaged game data files'
  'pyside6: for the included launcher script'
  'python-pyxdg: for the included launcher script'
  'mergerfs: required by the included launcher script to setup the correct file structure'
)
install=$pkgname.install
options=(!lto)
source=(
  git+https://github.com/RobertBeckebans/RBDOOM-3-BFG.git
  rbdoom-3-bfg.png
  rbdoom-3-bfg.desktop
  rbdoom-3-bfg-launcher
  rbdoom-3-bfg-launcher.desktop
  https://github.com/microsoft/DirectXShaderCompiler/releases/download/v1.8.2405/linux_dxc_2024_05_24.x86_64.tar.gz
)
noextract=(
  linux_dxc_2024_05_24.x86_64.tar.gz
)

pkgver() {
  cd RBDOOM-3-BFG
  printf "%s" "$(git describe --long --tags | sed 's/\([^-]*-\)g/r\1/;s/-/./g;s/^v//g')"
}

prepare() {
  cd RBDOOM-3-BFG
  git remote set-url origin https://github.com/RobertBeckebans/RBDOOM-3-BFG.git
  git submodule update --init --filter=tree:0 --recursive
}

build() {
  mkdir -p "$srcdir/dxc/"
  tar xvzf "$srcdir/linux_dxc_2024_05_24.x86_64.tar.gz" --directory "$srcdir/dxc/"
  chmod +x "$srcdir/dxc/bin/dxc"
  export PATH="$srcdir/dxc/bin:$PATH"

  export CC=clang
  export CXX=clang++
  cmake \
    -S RBDOOM-3-BFG/neo \
    -B build \
    -DCMAKE_BUILD_TYPE="Release" \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBINKDEC=ON \
    -DFFMPEG=OFF \
    -DWINDOWS10=OFF \
    -DUSE_DX12=OFF \
    -DUSE_VULKAN=ON \
    -DUSE_SYSTEM_RAPIDJSON=OFF \
    -DUSE_SYSTEM_ZLIB=OFF \
    -DUSE_PRECOMPILED_HEADERS=OFF \
    -GNinja \
    -Wno-dev
  cmake --build build --verbose
}

package() {
  install -dm755 "$pkgdir"/usr/share/games/doom3bfg
  cp -r RBDOOM-3-BFG/base "$pkgdir"/usr/share/games/doom3bfg/
  install -Dm755 -t "$pkgdir"/usr/bin/ build/RBDoom3BFG
  install -Dm755 -t "$pkgdir"/usr/bin/ rbdoom-3-bfg-launcher
  install -Dm644 -t "$pkgdir"/usr/share/applications/ rbdoom-3-bfg.desktop
  install -Dm644 -t "$pkgdir"/usr/share/applications/ rbdoom-3-bfg-launcher.desktop
  install -Dm644 -t "$pkgdir"/usr/share/pixmaps/ rbdoom-3-bfg.png
}

sha256sums=('SKIP'
            '0fb6a3bb9b47cad65d5012ba20dc9de3b1487f4ac1908ee847e6087511b7f09e'
            'f317d503d30ecc6e0a70e353cb020d2ea78ecb05d8d2e0250f39cfd8a4fe16a5'
            'a3c3006c254ce25ff890494acc8cfbfb88d406661f4468b56aaefc144227db21'
            'c083d028b5dd6f7c0d13d8d3f7a4b73a0d57d414890fea454eecbbf2dbcdac6d'
            '85d74a5f7e0ad339cae875b5baab04c38218d6ac33960b75f8780fd9d8447ee9')
